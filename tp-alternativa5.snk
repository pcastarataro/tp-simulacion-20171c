;declaración de tables y qtables
EDAD    	TABLE     M1,600,300,10      ;10 intervalos de 5 minutos, 1er. intervalo 10 minutos
*
*
* TABLA DE CANTIDAD PERSONAS EN EL BAR
*
INTBAR     TABLE     S$MESAS,0,1,10  ;10 intervalos de 1 CLIENTE, 1er intervalo 0 CLIENTES
*
*
* TABLA DE SUMA DE PERSONAS GONDOLAS
*
INTGOND    TABLE     S$GONDOLAS,0,5,10  ;10 intervalos de 5 CLIENTES, 1er intervalo 0 CLIENTES
*
*
* TABLA DE SUMA DE PERSONAS EN COLA DE CAJAS
*
TSUMCOL    TABLE     P$SCCAJ,0,1,5  ;5 intervalos de 1 CAJA, 1er intervalo 0 CAJAS
*
*
* TABLA DE PERSONAS EN COLA DE PROMOCION
*
TPROMOCION  QTABLE     PROMOCION,0,10,6   ;6 intervalos de 10 SEGUNDOS,1er intervalo 0 CLIENTES
*
*
* STORAGES DE MESAS, GONDOLAS Y PROMOCIONES
*
MESAS	  	STORAGE 10000 
GONDOLAS  	STORAGE 10000 
PROMOCION	STORAGE 2
*
*
*******************
* FUNCION PARA DETERMINAR EL TIPO DE CLIENTE
*******************
*
TIPO		FUNCTION RN1,D4
.20,1/.55,2/.7,3/1.,4
*
*
*******************
* EXPONENCIAL DADA EN EL ENUNCIADO
*******************
*
POISSON 	FUNCTION RN8,C24 ;exponencial negativa con valor medio igual a 1
0,0/.1,.104/.2,.222/.3,.355/.4,.509/.5,.69/.6,.915/.7,1.2/.75,1.38/.8,1.6/.84,1.83/.88,2.12/.9,2.3/.92,2.52/.94,2.81/.95,2.99/.96,3.2/.97,3.5/.98,3.9/.99,4.6/.995,5.3/.998,6.2/.999,7/.9998,8
*
*
*******************
* CANTIDAD MEDIA DE DEMORA EN CAJA
*******************
*
MEDIA_CAJA FUNCTION P$TIPO,D4
1,40/2,60/3,120/4,90
*
*
*******************
* DESVIO DE CANTIDAD MEDIA DE DEMORA EN CAJA
*******************
*
DESV_CAJA 	FUNCTION P$TIPO,D4
1,20/2,40/3,60/4,60
*
*
*******************
* BIFURCACION PARA SABER CUANTOS VAN AL BAR
*******************
*
BIF_BAR	FUNCTION P$TIPO,D4
1,50/2,350/3,800/4,400
*
*
*******************
* FUNCION ...
*******************
*
MAX_COLA	FUNCTION P$TIPO,D4
1,3/2,5/3,9/4,4
*
*
*******************
* SUMATIORIA DE PERSONAS EN TODAS LAS CAJAS
*******************
*
SUMCOLCAJAS VARIABLE Q1+Q2+Q3+Q4+Q5 
		INITIAL 	X$TOTAL_CJ,2
		INITIAL 	X$ARRIBO,90
	      INITIAL     X$CONTAR_GOND,0

CAJA_MIN	VARIABLE	RN5@2+1
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
	  	GENERATE 	X$ARRIBO,POISSON 
		ASSIGN 		TIPO,FN$TIPO
		ADVANCE		45,30
; VA AL BAR
		TRANSFER	FN$BIF_BAR,PROMOCION
		TRANSFER    	,BAR


; SUBSISTEMA BAR
BAR	  	ADVANCE 	90,30		; RECOGER COMIDA
	  	QUEUE 		CAJABAR
	 	SEIZE 		CAJABAR
	 	DEPART 		CAJABAR
	 	ADVANCE	50,25		; PAGAR COMIDA
	  	RELEASE	CAJABAR
	  	ENTER		MESAS
	  	ADVANCE	900,450		; COMER
	  	LEAVE 		MESAS
		TRANSFER	,PROMOCION


; SUBSISTEMA PROMOCION
PROMOCION	ADVANCE	45,15			; LLEGAR A PROMOCION
		TEST L 		Q$PROMOCION,FN$MAX_COLA,COMPRA
		QUEUE 		PROMOCION
		ENTER		PROMOCION
		DEPART		PROMOCION
		ADVANCE	60,15			; ATENCION PROMOCION
		LEAVE		PROMOCION
		TRANSFER	,COMPRA


; SUBSISTEMA COMPRA
COMPRA	ENTER GONDOLAS
		ADVANCE	900,300		; COMPRA
		LEAVE GONDOLAS
		SELECT NU	CAJA,1,X$TOTAL_CJ,,,NOHAY
		TRANSFER 	,SIGUE
NOHAY	COUNT  MIN	CAJA_MIN,1,X$TOTAL_CJ,,Q
		TEST	G 	P$CAJA_MIN,1,MIN
		ASSIGN		CAJA,V$CAJA_MIN
		TRANSFER	,SIGUE
MIN		SELECT MIN	CAJA,1,X$TOTAL_CJ,,Q,
SIGUE	QUEUE 		P$CAJA
		SEIZE 		P$CAJA
		DEPART 		P$CAJA
		ASSIGN 		DESVIO,FN$DESV_CAJA
		ADVANCE 	FN$MEDIA_CAJA,P$DESVIO
		RELEASE 	P$CAJA
		ADVANCE 	45,15	;SALIDA CAJA
		TABULATE  EDAD             ;TIEMPO EN EL SISTEMA
		TERMINATE


;SUBSISTEMA CANTIDAD CAJAS Y CONTROL PARA CONTAR DESPUES DE LA PRIMER HORA LA GENTE EN GONDOLAS
		GENERATE 	,,,1
		ADVANCE		3600
		SAVEVALUE	CONTAR_GOND,1
		ADVANCE		3600
		SAVEVALUE	ARRIBOS,50
		ADVANCE		7200
		SAVEVALUE	ARRIBOS,70
		TERMINATE


; SUBSISTEMA TIEMPO
TIEMPO		GENERATE 	3600
		TERMINATE 	1


;subsistema para contar los CLIENTES  que están DENTRO DEL BAR COMIENDO CADA 2 MINUTOS
        GENERATE  120
        TABULATE  INTBAR
        TERMINATE


;subsistema para contar los CLIENTES  que están EN LAS COLAS DE LAS CAJAS, CADA 2 MINUTOS, 
        GENERATE  120
	   ASSIGN 	  SCCAJ,V$SUMCOLCAJAS
        TABULATE  TSUMCOL
	   TERMINATE


;subsistema para contar los CLIENTES  que están GONDOLAS
        GENERATE  120
	   ASSIGN    FLAG,0
	   TEST E    P$FLAG,X$CONTAR_GOND,FIN
	   TABULATE  INTGOND
FIN	   TERMINATE
